buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.9.10"
}

version "4.0.0"
group "org.grails.plugins"

apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: "org.grails.grails-gsp"

ext {
    grailsVersion = project.grailsVersion
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://repo.spring.io/milestone/" }
    maven { url "http://oss.sonatype.org/content/repositories/snapshots" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.springframework.boot:spring-boot-starter-tomcat"

    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile 'org.grails:grails-web-testing-support:2.0.0'

    compile 'redis.clients:jedis:2.9.0'
    compile 'com.google.code.gson:gson:2.6.1'
}

jar {
  exclude "application.groovy"
  exclude "src/test/projects/**"
}

bintray {
    user = System.getenv("BINTRAY_USER") ?: project.hasProperty("bintrayUser") ? project.bintrayUser : '' 
    key = System.getenv("BINTRAY_KEY") ?: project.hasProperty("bintrayKey") ? project.bintrayKey : '' 
    publications = ['maven']
    pkg {
        repo = 'grails-plugins'
        userOrg = 'ctoestreich'
        name = "redis"
        desc = "Grails Redis plugin"
        websiteUrl = "http://grails.org/plugin/redis"
        issueTrackerUrl = "https://github.com/grails-plugins/grails-redis/issues"
        vcsUrl = "https://github.com/grails-plugins/grails-redis"
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}


bintrayUpload.dependsOn sourcesJar, javadocJar

apply from: './gradle/artifactory.gradle'
